source ${HOME}/bin/_common.zsh

# --- Paths / Globals

alias -g ddw='"$HOME/Downloads"'
alias -g ddc='"$HOME/Documents"'
alias -g dpr='"$HOME/Projects"'
alias -g ddd='"$HOME/Library/Developer/Xcode/DerivedData/"'

alias -g X="| xargs"
alias -g X0="| xargs -0"
alias -g XG="| xargs -0 grep -E"
alias -g CDL="| colordiff | less"
alias -g PBC="| pbcopy"
alias -g H='| head'
alias -g T='| tail'
alias -g G='| grep -E'
alias -g GI='| grep -Ei'
alias -g L="| less"
alias -g LL="2>&1 | less"
# alias -g CA="2>&1 | cat -A"
alias -g NE="2> /dev/null"
alias -g NUL="> /dev/null 2>&1"
alias -g SU="| sort | uniq"
alias -g XCB="| xcbeautify -q --disable-logging"

# --- System

flush_dns() {
  sudo killall -HUP mDNSResponder
  sudo killall mDNSResponderHelper
  sudo dscacheutil -flushcache
}

notify() {
  if /usr/bin/which -s noti ; then
    _exec noti "$@"
  else
    _exec "$@"
  fi
}

cz() {
    chezmoi "$@"
}

alias '??'="llm cmd -m gemini-2.5-flash"

alias ced="cz edit --apply"
alias cap="cz apply"
alias cad="cz add"
alias cra="cz re-add"
alias cup="open ~/bin/update_chezmoi.terminal"
alias cma="cz merge-all"
alias cfo="cz forget"

alias hyf="hyperfine -m 3"

alias src='source ~/.zshrc'
alias alg='alias | grep'

alias ez="eza --group-directories-first --icons=auto --smart-group --git -h"
alias e="ez -1"
alias el="ez -l"
alias ea="el -a"
unalias l ll la 2> /dev/null
alias l=e ll=el la=ea
alias lt="l -s modified -r"
alias llt="ll -m -s modified -r"

alias cal="gcal -s 1"

alias proxyman-cli=/Applications/Setapp/Proxyman.app/Contents/MacOS/proxyman-cli

br() {
    brew "$@"
}

brup() {
  if [[ -z "$1" ]]; then
    br update
  else
    br upgrade "$@"
  fi
}

alias brs="br search"
alias brfo="br info"
alias brls="br list"
alias brin="br install"
alias brinc="br install --cask"
alias brun="br uninstall"
alias brunc="br uninstall --cask"

alias lsg="lsof | grep -i"
alias lsig="lsof -i | grep -i"
alias psg="ps aux | grep -i"
# alias top="glances --process-short-name"
alias dfh="dfc -blw"
alias duh="du -csh"
alias du="du -h"

x86() {
  arch -x86_64 "$@"
}

alias m=make
alias j=just

o() {
  if [[ -z "$1" ]]; then
    open .
  else
    open "$@"
  fi
}

alias xdq="xattr -d com.apple.quarantine"
alias xf="xmllint --format"

alias v=vim
alias g=gvim
gvim() {
    vimer -t "$@"
}

c() {
  if [[ -z "$1" ]]; then
    code-insiders .
  else
    code-insiders "$@"
  fi
}

z() {
  if [[ -z "$1" ]]; then
    zed .
  else
    zed "$@"
  fi
}

alias diff=colordiff


alias diff=colordiff
alias diffstat="diffstat -f3 -C"

cat() {
  type glow >/dev/null 2>&1
  has_glow=$?

  if [[ $has_glow && -z "$2" && "$1:t:e" == "md" ]]; then
    glow -p $1
  else
    bat "$@"
  fi
}

cata() {
    cat --style=header "$@"
}

clear_ascii() {
    cat "$@" | sed -E "s/"$'\E'"\[([0-9]{1,3}((;[0-9]{1,3})*)?)?[m|K]//g"
}

dm() {
  if [[ -d /Applications/Dash.app || /Applications/SetApp/Dash.app ]]; then
    url=$(printf %s "manpages:$1"|jq -sRr @uri)
    open "dash://${url}"
  else
    /usr/bin/man "$@"
  fi
}

man() {
    tldr "$@" || dm "$@"
}

# --- zsh

_shell_init_files=${HOME}/{bin/_common.zsh,.{zshenv,zprofile,zshrc,aliases,zimrc},.config/{Brewfile,bonkey.omp.json,mise/config.toml}}

cvz() {
    ced $(eval "echo $_shell_init_files")
    exit
}

cvzl() {
  $EDITOR $(eval "echo $_shell_init_files")
  source ${HOME}/.zshrc
}

# --- Network

alias h="http"
alias hv="h -v"
alias hf="h --follow"
alias hvf="hv --follow"

alias curl="curlie"

alias mtr="sudo mtr"
alias p1="ping 1.1.1.1"
alias m1="mtr 1.1.1.1"
alias m8="mtr 8.8.8.8"
alias mg="mtr google.com"

ping() {
  if type gping 2>&1 > /dev/null ; then
    gping -s -b 10 --vertical-margin 5 --horizontal-margin 10 "$@"
  else
    /sbin/ping "$@"
  fi
}

alias host=doggo
alias dig=doggo

# --- LLM

lgf() {
    llmm gemini-2.5-flash "$@"
}

lgp() {
    llmm gemini-2.5-pro "$@"
}

lgt() {
    llmm gemini-2.0-flash-thinking-exp-01-21 "$@"
}

lgl() {
    llmm gemini-2.0-flash-lite "$@"
}

llmm() {
    llm -m "$@"
}


# --- Search

f() { fd --follow "$@" }
f0() { f "$@" --print0 }

ff() { f --type f "$@" }
ff0() { f0 --type f "$@" }

alias fda="fd -HI"

alias ag='ag --color-path "3;32" --color-line-number "0;36" --color-match "1;31;40"'
alias agw="ag -w"
alias aga="ag -u"
alias agh="ag -U --hidden"
alias agl="ag -l"


agz() {
  ag "$@" $_shell_init_files
}

ags() {
  ag --ignore=Carthage --ignore=Pods --ignore=\*.framework --objc --objcpp --swift "$@"
}

agsw() {
  ags -w "$@"
}

agst() {
  agsw " *(protocol|actor|struct|typealias|class) +$1"
}

agc() {
  ag -c "$@" | awk -F: 'BEGIN { c = 0} { c += $2 } END { print "found:", c}'
}

# --- Node / nvm

nvm-init() {
  export NVM_DIR="$HOME/.nvm"
  [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
  [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
}

# --- Media

unalias gm 2> /dev/null

img_diff() {
  img1=$1
  img2=$2
  dst="diff_${img1}-${img2}.png"

  _exec "compare -verbose -density 300 '$img1' '$img2' -compose src '$dst'"
}

montage() {
  tile=$1
  [[ -z "$3" ]] && echo "montage <XxY> <file...> <output_file>" && return
  shift
  gm montage +frame +shadow +label -tile $tile -geometry +1+1 "$@"
}

ff_silent_sound() {
  for i in "$@" ; do
    dst=${i:r}-nosound.${i:e}
    echo ffmpeg -i "$i" -f lavfi -i anullsrc=channel_layout=stereo:sample_rate=44100 -shortest -c:v copy -c:a aac $dst
  done
}

ff_m4v() {
  ff_format m4v "$@"
}

ff_webm() {
  ff_format webm "$@"
}

ff_format() {
  local i
  emulate -L zsh
  format=$1
  shift

  if [[ -z "$1" ]]; then
    log_error "Please provide output format file extension (e.g. 'webm') and files to convert"
    return 1
  fi

  for i in "$@" ; do
    ffmpeg -y -i "$i" "${i:r}.${format}" && rm -f "$i"
  done
}


ff_fps() {
  local i
  fps=$1 ; shift
  for i in "$@" ; do
    dst=${i:r}-fps${fps}.${i:e}
    ffmpeg -i "$i" -r $fps -qscale 0 $dst
  done
}

ff_trim() {
  emulate -L zsh

  help="Help: $0 <input_movie> <output_movie> <start> <length>"

  test -z "$1" && echo "${help}" && return 1
  test -z "$2" && echo "No OUTPUT given. ${help}" && return 2
  test -z "$3" && echo "No START given. ${help}" && return 3
  test -z "$4" && echo "No LENGTH given. ${help}" && return 4

  in=$1
  out=$2
  ine=${in:e}
  outn=${out:r}
  start=$3
  len=$4

  ffmpeg -i "${in}" -codec copy -y -ss $start -t $len "${outn}.${ine}"
}

ff_gif() {
  input=$1
  output="${input:r}.gif"

  fps=${2:-10}
  width=${3:-600}
  height=${4:--1}
  ffmpeg -i "$input" -filter_complex "[0:v] fps=${fps},scale=w=${width}:h=${height},split [a][b];[a] palettegen=stats_mode=single [p];[b][p] paletteuse=new=1" -f gif -y -hide_banner "${output}"
}

# --- iOS

alias bef="bue fastlane"

alias xcs="xcodes select"
xcs15() { xcs '15.4' }
xcs16() { xcs '16.4' }

alias xcsv="xcrun xcodebuild -version | head -1 | sed 's/.*Xcode \(.*\)/\1/'"
alias xcss="xcodes installed | grep Selected"

alias xcsc="xcrun simctl"
alias xcscl="xcsc list devices"
alias xcscld="xcscl devices"
alias xcsclr="xcscl runtimes"
alias xcscb="xcsc list|grep boot -i"

alias xcsclog="xcrun simctl spawn booted log stream --color always --debug --style compact"

xcsclogg() {
  xcsclog --predicate "$1"
}

xcsclogge() {
  xcsclog "eventMessage contains '$1'"
}

xck() {
  _exec "pkill -9 Xcode"
}

xcresolve() {
  proj=$1
  test -n "$proj" && shift
  scheme=$1
  test -n "$scheme" && shift

  if [[ ! -d $proj ]]; then
    echo "Xcode project/workspace '$proj' not found"
    return
  fi

  if [[ "$proj" == *.xcworkspace ]]; then
    arg="-workspace"
    if [[ -z "$scheme" ]]; then
      scheme=$(basename $proj .xcworkspace)
    fi
  else
    arg="-project"
    if [[ -z "$scheme" ]]; then
      scheme=$(basename $proj .xcodeproj)
    fi
  fi

  if [[ "$scheme" == "-l" || "$scheme" == "-list" || "$scheme" == "--list" ]]; then
    log_message "Listing avilable schemes..."
    _exec "xcrun xcodebuild -destination 'generic/platform=iOS Simulator' -list $arg $proj"
    return
  fi

  _exec "pkill -9 Xcode || true"
  _exec "find $proj -name Package.resolved -print -delete"
  _exec "xcrun xcodebuild -destination 'generic/platform=iOS Simulator' clean $arg $proj -scheme $scheme"
  _exec "xcrun xcodebuild -destination 'generic/platform=iOS Simulator' -resolvePackageDependencies $arg $proj -scheme $scheme"
}

xcb() {
  proj=$1
  test -n "$proj" && shift
  scheme=$1
  test -n "$scheme" && shift

  if [[ ! -d $proj ]]; then
    echo "$proj not found"
    return
  fi

  if [[ "$proj" == *.xcworkspace ]]; then
    arg="-workspace"
    if [[ -z "$scheme" ]]; then
      scheme=$(basename $proj .xcworkspace)
    fi
  else
    arg="-project"
    if [[ -z "$scheme" ]]; then
      scheme=$(basename $proj .xcodeproj)
    fi
  fi

  _exec "xcrun xcodebuild $arg $proj -scheme $scheme -destination 'generic/platform=iOS Simulator' $@ XCB"
}

xcreset() {
  xck

  _exec "rm -rf \"$HOME/Library/org.swift.swiftpm\""
  _exec "fd -t d remotes '$HOME/Library/Caches/org.swift.swiftpm' -x rm -rf"
  _exec "fd -t d --full-path 'SourcePackages/repositories/.*/remotes$' '$HOME/Library/Developer/Xcode/DerivedData/' -x rm -rf"
}

ticket_from_branch() {
  local BRANCH TICKET
  BRANCH=$(git symbolic-ref --short HEAD)
  (echo $BRANCH | grep -q "/") || return
  TICKET=$(echo $BRANCH | sed -E 's#.*/([A-Z]+-[0-9]+)[^/]*#\1#')
  [[ -z "$TICKET" ]] && return
  echo $TICKET
}

find_xcode_proj() {
  local xcode_proj

  if [[ -e "$1" ]]; then
    xcode_proj=$1
  else
    projects=(*.{xcworkspace,xcodeproj}(N) Package.swift(N))
    xcode_proj=$projects[1]
  fi

  test -z "$xcode_proj" && echo "No xcworkspace/xcodeproj file provider, nor found in the current directory." && return 1
  echo $xcode_proj
}

alias xc > /dev/null && unalias xc
xc() {
  local xcode_proj xcode

  xcode_proj=$(find_xcode_proj "$1")
  [ $? -ne 0 ] && echo "$xcode_proj" && return

  if [[ -f .xcode-version ]]; then
    xcode_version=$(cat .xcode-version)
    (xcrun xcodebuild -version | head -1 | grep -q ${xcode_version}) || \
      (echo "Selecting Xcode ${xcode_version}..." && xcversion select $xcode_version)
  fi
  xcode="$(xcode-select -p)"
  xcode="${xcode:s/\/Contents\/Developer/}"
  echo "Opening ${xcode_proj} with ${xcode} ($fg_bold[green]$(xcsv)${reset_color})"

  open -a "$xcode" "${xcode_proj}"
}

xco() {
  dir=$1
  project=$2

  if [[ ! -e $project ]]; then
    cd $dir
  fi

  log_message "Dir: $(pwd)"

  xc $project
}

_pod() {
  if [[ -f "Gemfile" || -f "../Gemfile" ]]; then
    _exec "bundle check || bundle install"
    _exec "bundle exec pod $@"
  else
    _exec "pod $@"
  fi
}

alias poi="_pod install"
alias poir="_pod install --repo-update"
alias pou="_pod update"
alias poun="_pod update --no-repo-update"
alias poo="_pod outdated"
alias pos="_pod search"

# --- git

alias gf > /dev/null && unalias gf
gf() {
  fork .
}

gl-pr-changelog() {
  git log --pretty='format:* %s' "$@" | sd '[A-Z]{3,}-[0-9]+: ' ''
}

GWab() {
    branch=$1
    dir_name="$(echo "$branch" | sd '^[^/]*/?' '')"

    root_repo_dir=$(git worktree list --porcelain | awk '/^worktree /{print $2}' | awk '
      NR==1{common=$0; next}
      {
        n=split(common, a, "/")
        m=split($0, b, "/")
        max=(n<m?n:m)
        out=""
        for(i=1;i<=max;i++) {
          if(a[i]==b[i]) {
            out = out (i==1 ? "" : "/") a[i]
          } else {
            break
          }
        }
        common=out
      }
      END{print common}
    ')

    branch_dir="$root_repo_dir/$dir_name"

    _exec "echo git worktree add '$branch_dir' '$branch'"

    # cd "$branch_dir"
}

gsw() {
    git update-index --skip-worktree "$@"
}

alias gip="gitup"
alias rc="retcon"

alias gl="git pull --autostash"
# alias gd="git diff"
alias gdn="gd --names-only"
alias gst="git status"

alias gco="git checkout"
alias gcm="gco main"

gcai() {
    local prompt="Create one-liner commit message from the git diff changes. \
    Make it useful, as high level as possible. \
    Never use backticks since they can be passed to a shell accidentally"
    local model="gemini-2.5-flash"

    local add_changes=false
    local no_push_pull=false
    local check_confirmation=false
    local dry_run=false
    local args=()
    local diff_args=("-w" "--cached")

    while [[ $# -gt 0 ]]; do
        case "$1" in
            -a|--add)
                add_changes=true
                shift
                ;;
            -np|--no-push-pull)
                no_push_pull=true
                shift
                ;;
            -d|--dry-run)
                dry_run=true
                shift
                ;;
            -c|--check)
                check_confirmation=true
                shift
                ;;
            -m|--model)
                model="$2"
                shift 2
                ;;
            *)
                args+=("$1")
                shift
                ;;
        esac
    done

    if [[ "$dry_run" = true ]]; then
        log_message "Dry run enabled. Only commit message will be generated"
    fi


    if [[ -z "$(llm models list -m "$model" 2>/dev/null)" ]]; then
        log_error "Model '$model' does not exist"
        return 1
    fi

    if [[ "$no_push_pull" = false && "$dry_run" = false ]]; then
        log_message "Pulling latest changes from remote.."
        _exec "git pull --autostash"
    fi

    if [[ "$add_changes" = true && "$dry_run" = false ]]; then
        log_message "Staging all changes.."
        _exec "git add ."
    else
        diff_args+=("HEAD")
        log_message "Skipping pull..."
    fi

    _exec "git diff --stat ${diff_args[@]}"

    if [[ "$dry_run" = false ]] && git diff --cached --quiet; then
        log_message "No changes staged. Please stage your changes before committing"
        return
    fi

    log_message "Generating commit message with ${model}.."
    local commit_msg

    while true; do
        commit_msg=$(git diff "${diff_args[@]}" "${args[@]}" | llm -x -m "$model" -s "$prompt")

        if [[ "$dry_run" = true ]]; then
            log_message "Commit message would be '$commit_msg'"
            break
        fi

        if [[ "$check_confirmation" = true && "$dry_run" = false ]]; then
            log_message "Generated commit message: '$commit_msg'"
            echo -n "Proceed with this commit message? [y/n/r]: "
            read -k 1 response
            echo # Add a newline after the response
            if [[ "$response" =~ ^[Yy]$ ]]; then
                break
            elif [[ "$response" =~ ^[Rr]$ ]]; then
                log_message "Regenerating commit message..."
                continue
            else
                log_message "Commit aborted."
                return
            fi
        else
            break
        fi
    done

    if [[ "$dry_run" = false ]]; then
        log_message "Committing changes.."
        _exec "git commit -m \"$commit_msg\""
    fi

    if [[ "$no_push_pull" = false && "$dry_run" = false ]]; then
        log_message "Pushing commit to remote.."
        _exec "git push"
    else
        log_message "Skipping push"
    fi
}

alias gl1m="git lg --merges"
alias gl1="git lg --no-merges"

alias gcl="git clone --recurse-submodules --progress"

alias gds="gd --stat"
alias gdn="gd --name-only"

alias gdt="git difftool"
alias gdtd="git difftool main"

alias gcam="git commit -am"
alias gls="git ls-files"
alias glsm="gls -m"
alias gpo="gpu origin"

git-merged-show() {
  git branch --merged develop|tr -d ' '| grep -vE '^(develop|master|main)$'
}

unalias gpu 2> /dev/null
gpu() {
  test -z "$1" && echo "$0 <remote_name>" && return
  git push -u $1 $(git name-rev --name-only HEAD)
}

git-retrigger-checks() {
  git commit -m "Retrigger checks" --allow-empty && git push
}

git_pull_and_back() {
  local merging_branch stashed current_branch

  if [[ -z "$1" ]]; then
    log_error "Please provide branch name for git_pull_and_back"
    return 1
  fi
  merging_branch=$1; shift

  # Get current branch for better error handling
  current_branch=$(git symbolic-ref --short HEAD 2>/dev/null)
  if [[ -z "$current_branch" ]]; then
    log_error "Not on a git branch"
    return 1
  fi

  # Check if target branch exists
  if ! git show-ref --verify --quiet "refs/heads/$merging_branch"; then
    log_error "Branch '$merging_branch' does not exist"
    return 1
  fi

  # Check if the branch is checked out in another worktree
  is_in_worktree=""
  if git worktree list --porcelain | grep -q "branch refs/heads/$merging_branch"; then
    is_in_worktree="true"
    log_message "Branch '$merging_branch' is checked out in another worktree. Will fetch updates instead of switching."
  fi

  # Stash changes if any exist
  if ! git diff-index --quiet HEAD --; then
    stashed=$(git stash push --include-untracked -m "Temporary stash for git_pull_and_back")
    log_message "Stashed changes: $stashed"
  else
    stashed=""
  fi

  # Execute the git operations with proper error handling
  if [[ -n "$is_in_worktree" ]]; then
    log_message "Fetching latest changes for $merging_branch from remote"
    if git fetch origin "$merging_branch"; then
      log_message "Successfully fetched latest changes for $merging_branch"
      # Now run the additional commands in the current directory using origin/branch
      modified_commands=$(echo "$*" | sed "s/\\b$merging_branch\\b/origin\\/$merging_branch/g")
      if eval "$modified_commands"; then
        log_message "Successfully ran extra commands in current directory"
      else
        log_error "Extra commands failed in current directory"
        [[ -n "$stashed" ]] && git stash pop 2>/dev/null
        return 1
      fi
    else
      log_error "Git fetch failed for $merging_branch"
      [[ -n "$stashed" ]] && git stash pop 2>/dev/null
      return 1
    fi
  else
    log_message "Switching to $merging_branch, pulling latest, then returning to $current_branch"
    if git checkout "$merging_branch" && \
       git pull && \
       git checkout "$current_branch" && \
       eval "$*"; then
      log_message "Successfully completed git operations"
    else
      log_error "Git operations failed, attempting to return to original state"
      git checkout "$current_branch" 2>/dev/null
      [[ -n "$stashed" ]] && git stash pop 2>/dev/null
      return 1
    fi
  fi

  # Restore stashed changes if any were stashed
  if [[ -n "$stashed" && "$stashed" != "No local changes to save" ]]; then
    if git stash pop; then
      log_message "Restored stashed changes"
    else
      log_error "Failed to restore stashed changes"
      return 1
    fi
  fi
}

gpm() {
  src_branch=${1:-main}
  git_pull_and_back $src_branch "git merge --no-edit $src_branch"
}

gpr() {
  src_branch=${1:-main}
  git_pull_and_back $src_branch "git rebase $src_branch"
}

gicpa() {
  git add -A .

  if [[ -n "$1" ]]; then
    msg=$1
  else
    msg="Changed $(git diff --cached --name-only | uniq | tr '\n' ' ')"
  fi

  gcam "$msg" && git pull && git push
}

# -- Ruby

bu() {
  _exec "bundle $@"
}

buc() {
  _exec "bundle check || bundle install"
  bu $@
}

alias bue="buc exec"
alias buu="bu update"
alias buo="bu outdated"
alias bui="bu install"

# -- Jira

jira-ticket-in-branch() {
  local git_branch
  git_branch=$(git symbolic-ref --short HEAD 2> /dev/null)
  (echo $git_branch | grep -qE "[A-Z]{3,}-[0-9]+") || return
  echo $git_branch | sd '.*?([A-Z]{3,}-[0-9]+).*' '$1'
}

jira-ticket-summary() {
  local ticket summary
  ticket=$(jira-ticket-in-branch)
  if [[ -n "${ticket}" ]]; then
    # --verify='' to skip SSL errors with Netskope
    summary=$(http --verify='' GET "${JIRA_SERVER}/rest/api/3/issue/${ticket}" --auth "${JIRA_USER}:${JIRA_API_TOKEN}" | jq -r '.fields.summary')
    echo "${ticket}: ${summary}"
  else
    echo "No jira ticket found."
  fi
}

jira-extract-tickets() {
  local i
  for i in "$@" ; do
    echo "$i" | grep -E '[A-Z0-9]+[0-9]+' | sd '([A-Z0-9]+-[0-9]+)' '>>>>>$1<<<<<'|sd '.*>>>>>' '' | sd '<<<<<.*' ''
  done | sort -u
}

_git-branches-in-jira() {
    jira ls -q "key in ($(jira-extract-tickets "$@" | sd '\n' ' '|xargs|tr ' ' ','))"
}

git-branches-in-jira-all() {
  _git-branches-in-jira $(git branch)
}

git-branches-in-jira-merged() {
  _git-branches-in-jira $(git-merged-show)
}

# vim:set ft=zsh:
